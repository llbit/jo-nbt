plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
  id 'jacoco'
}

version = '1.3.0'
group = 'se.llbit'
archivesBaseName = 'jo-nbt'

// Source level is JDK 7 because we use AutoCloseable.
sourceCompatibility = targetCompatibility = '1.7'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'junit:junit:4.11'
}

jar.manifest.attributes 'Main-Class': 'se.llbit.nbt.DumpNBT'

test.inputs.dir file('testfiles')

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
  }
}

check.dependsOn jacocoTestReport

compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

javadoc {
  options.encoding = 'UTF-8'
  options.addStringOption('Xdoclint:none', '-quiet')
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'jo-nbt'
      from components.java

      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'jo-nbt'
        description = 'Java NBT library.'
        url = 'https://github.com/llbit/jo-nbt'
        licenses {
          license {
            name = 'Modified BSD License'
            url = 'http://opensource.org/licenses/BSD-3-Clause'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            name = 'Jesper Ã–qvist'
            email = 'jesper@llbit.se'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/llbit/jo-nbt.git'
          url = 'https://github.com/llbit/jo-nbt'
        }
      }
    }
  }

  repositories {
    maven {
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }
    }
  }
}
